plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.epages.restdocs-api-spec' version '0.19.2' apply false
}



allprojects {
    group = 'com.practice'
    version = '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
    }
    
    // Disable bootJar by default, enable only in core-api
    bootJar { enabled = false }
    jar { enabled = true }
}

// Configure specific modules
project(':core:core-api') {
    bootJar { enabled = true }
    jar { enabled = false }
}

// Intermediate modules shouldn't build jars
project(':core') {
    bootJar { enabled = false }
    jar { enabled = false }
}
project(':storage') {
    bootJar { enabled = false }
    jar { enabled = false }
}
project(':support') {
    bootJar { enabled = false }
    jar { enabled = false }
}

bootJar { enabled = false }
jar { enabled = false }
