plugins {
    id 'com.epages.restdocs-api-spec'
}

openapi3 {
    server = 'http://localhost:8080'
    title = '송금 서비스 API'
    description = '송금 서비스 Documentation'
    version = '0.1.0'
    format = 'yaml'
}



tasks.register('copyOasToClasses', Copy) {
    dependsOn 'openapi3'
    from "$buildDir/api-spec"
    into "$buildDir/resources/main/static"
}

tasks.named('bootRun') {
    dependsOn 'copyOasToClasses'
    classpath += files("$buildDir/resources/main")
}

tasks.named('bootJar') {
    dependsOn 'copyOasToClasses'
}

tasks.named('resolveMainClassName') {
    dependsOn 'copyOasToClasses'
}

dependencies {
    implementation project(':core:core-enum')
    implementation project(':storage:db-core')
    implementation project(':support:logging')

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework:spring-tx'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    // API Documentation
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.19.2'
}

test {
    testLogging {
        showStandardStreams = true
    }
}
